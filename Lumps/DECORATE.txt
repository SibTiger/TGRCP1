ACTOR TGR_TerKlassEins
{
	Radius 2
	Height 4
	+NOBLOCKMAP 
	+MISSILE 
	+DROPOFF
	+NOTELEPORT
	+LOWGRAVITY
	+CANNOTPUSH
	+DONTSPLASH
	+DONTBLAST
	States
	{
	Spawn:
		TFA1 ABC 8
		TFA1 D 16
		Stop
	Death:
		TFA1 D 10
		Stop
	}
}
ACTOR TGR_TerKlassZwei
{
	+NOBLOCKMAP
	+NOCLIP
	+NOGRAVITY
	+DONTSPLASH
	+DONTBLAST
	States
	{
	Spawn:
		TFA2 ABCDEFG 5
		Stop
	}
}
ACTOR TGR_TerKlassDrei
{
	Radius 2
	Height 4
	+NOBLOCKMAP 
	+MISSILE 
	+DROPOFF
	+NOTELEPORT
	+LOWGRAVITY
	+CANNOTPUSH
	+DONTSPLASH
	+DONTBLAST
	States
	{
	Spawn:
		TFA3 ABC 8
		TFA3 D 16
		Stop
	Death:
		TFA3 D 10
		Stop
	}
}
ACTOR TGR_TerKlassVier
{
	Radius 2
	Height 4
	+NOBLOCKMAP 
	+MISSILE 
	+DROPOFF
	+NOTELEPORT
	+LOWGRAVITY
	+CANNOTPUSH
	+DONTSPLASH
	States
	{
	Spawn:
		TFA4 ABCD 8
		Stop
	Death:
		TFA4 D 6
		Stop
	}
}
ACTOR TGR_TerKlassFuenf
{
	+NOBLOCKMAP
	+NOCLIP
	+NOGRAVITY
	+DONTSPLASH
	States
	{
	Spawn:
		TFA5 ABCD 6
		Stop
	}
}

// ===============================================
//              REALM667 Resources
// ===============================================

//BFG10k
//Credits: The SkullTag Team, Eriance, and Ghastly_Dragon.
Actor STBFG10K : DoomWeapon 5013
{
  Radius 20
  Height 20
  Weapon.Selectionorder 2800
  Weapon.AmmoUse 5
  Weapon.AmmoGive 40
  Weapon.AmmoType "Cell"
  Weapon.Kickback 100
  Weapon.SlotNumber 7
  Inventory.Pickupmessage "$PICKUP_BFG10K"
  Obituary "$OB_BFG10K"
  +Weapon.NoAutoFire
  Decal BFGLightning
  States
  {
  Spawn:
    BFG2 A -1
    Stop
  Ready:
    BG2G A 0 A_PlaySound ("Weapons/BFG10KIdle")
    BG2G A 1 A_WeaponReady
    BG2G A 1 A_WeaponReady
    BG2G A 1 A_WeaponReady
    BG2G B 1 A_WeaponReady
    BG2G B 1 A_WeaponReady
    BG2G B 1 A_WeaponReady
    BG2G C 1 A_WeaponReady
    BG2G C 1 A_WeaponReady
    BG2G C 1 A_WeaponReady
    BG2G D 1 A_WeaponReady
    BG2G D 1 A_WeaponReady
    BG2G D 1 A_WeaponReady
    Loop
  Deselect:
    BG2G E 1 A_Lower
    Loop
  Select:
    BG2G E 1 A_Raise
    Loop
  Fire:
    BG2G E 20 A_PlaySound("Weapons/BFG10KF")
    BG2G F 4
    BG2G G 1
    BG2G H 1
    BG2G I 1
    BG2G J 1
    Goto Hold
  Hold:
    BG2G K 2 A_GunFlash
    BG2G L 2 A_FireBullets(0, 0, 1, 0, "STBFG10kShot")
    BG2G M 2
    BG2G N 0 A_ReFire
    BG2G O 35 A_PlaySound("Weapons/BFG10KCool")
    Goto Ready
  Flash:
    TNT1 A 2 Bright A_Light1
    TNT1 A 3 bright
    Goto LightDone
  }
}


Actor STBFG10kShot
{
  Radius 11
  Height 8
  Speed 20
  Damage 160
  DamageType BFG10K
  +NoBlockMap
  +NoGravity
  +ActivateImpact
  +ActivatePCross
  +NoTeleport
  +PuffOnActors
  +PuffGetsOwner
  +ForceRadiusDmg
  Renderstyle Add
  Alpha 0.75
  SeeSound "Weapons/BFG10KX"
  AttackSound "Weapons/BFG10KX"
  Obituary "$OB_BFG10K"
  States
  {
  Spawn:
    BFE1 A 0 Bright
    BFE1 A 3 Bright A_Explode(160, 128, 1)
    BFE1 BCDEF 3 Bright
    Stop
  }
}

//GrenadeLauncher
//Credits: The SkullTag Team, and Ghastly_Dragon.
Actor STGrenadeLauncher : Weapon 5011
{
  Radius 20
  Height 16
  Weapon.SelectionOrder 2500
  Weapon.SlotNumber 5
  +WEAPON.NOAUTOFIRE
  +WEAPON.NOAUTOAIM
  Weapon.AmmoUse 1
  Weapon.AmmoGive 2
  Weapon.AmmoType "RocketAmmo"
  Weapon.Kickback 100
  Inventory.PickupMessage "$PICKUP_GRENADELAUNCHER"
  Obituary "$OB_GRENADE"
  States
  {
  Spawn:
    GLAU A -1
    Stop
  Ready:
    GRLG A 1 A_WeaponReady
    Loop
  Deselect:
    GRLG A 1 A_Lower
    Loop
  Select: 
    GRLG A 1 A_Raise
    Loop
  Fire: 
    GRLG B 8 A_GunFlash
    GRLG B 12 A_FireCustomMissile("STGrenade", 0, 1, 0, 0, 0, 6.328125)
    GRLG B 0 A_ReFire
    Goto Ready 
  Flash: 
    GRLF A 3 Bright A_Light1
    GRLF B 4 Bright
    GRLF C 4 Bright A_Light2
    GRLF D 4 Bright A_Light2
    Goto LightDone
  }
}

Actor STGrenade
{
  Radius 8
  Height 8
  Speed 25
  Damage 20
  Gravity 0.25
  Projectile
  -NoGravity
  +DoomBounce
  +DehExplosion
  +GrenadeTrail
  +ExplodeOnDeath
  SeeSound "Weapons/GrBnce"
  DeathSound "Weapons/GrenLX"
  Obituary "$OB_GRENADE"
  DamageType Fire
  States
  {
  Spawn:
    SGRN A 0 Bright
    SGRN A 0 Bright A_StopSound
    SGRN A 0 Bright A_PlaySound("Weapons/GrenLF")
    SGRN A 1 Bright 
    Goto Spawn+3
  Death:
    MISL B 0 Bright A_ChangeFlag("NoGravity", 1)
    MISL B 8 Bright A_Explode
    MISL C 6 Bright
    MISL D 4 Bright
    stop
  }
}

//Minigun
//Credits: The SkullTag Team, and Ghastly_Dragon.
ACTOR STMinigun : Weapon 5014
{
	Weapon.AmmoType "Clip"
	Weapon.AmmoGive 20
	Weapon.AmmoUse 1
	Weapon.SlotNumber 4
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "$PICKUP_MINIGUN"
	Obituary "$OB_MINIGUN"
	AttackSound "weapons/minigunfire"
	States
	{
		Spawn:
		MNGN A -1
		Loop
		Ready:
		MNGG A 1 A_WeaponReady
		Loop
		Deselect:
		MNGG A 1 A_Lower
		Loop
		Select:
		MNGG A 1 A_Raise
		Loop
		Fire:
		MNGG A 0 A_GunFlash
		MNGG A 2 A_FireBullets(5, 1, 1, 5)
		MNGG B 2 A_FireBullets(5, 1, 1, 5)
		MNGG B 0 A_ReFire
		MNGG B 2
		MNGG A 5
		MNGG B 8
		MNGG A 11
		MNGG B 14
		MNGG A 17
		goto Ready
		Flash:
		MNGF A 3 BRIGHT A_Light1
		MNGF B 3 BRIGHT A_Light0
		Stop
	}
}

//Railgun
//Credits: The SkullTag Team, and Ghastly_Dragon.
Actor STRailgun : DoomWeapon 5012
{
  Radius 20
  Height 16
  Weapon.Selectionorder 100
  Weapon.AmmoUse 10
  Weapon.AmmoGive 40
  Weapon.AmmoType "Cell"
  Weapon.SlotNumber 6
  Inventory.Pickupmessage "$PICKUP_RAILGUN"
  Obituary "$OB_RAILGUN"
  Decal RailScorch
  States
  {
  Ready:
    RLGG A 1 A_WeaponReady
    Loop
  Deselect:
    RLGG A 1 A_Lower
    Loop
  Select:
    RLGG A 1 A_Raise
    Loop
  Fire:
    RLGG E 12 A_FireRailgun
    RLGG F 6 A_CheckForReload(4, "Reloaded")
    RLGG GHIJK 6
    RLGG L 6 A_ResetReloadCounter
  Reloaded:
    RLGG A 6
    RLGG M 0 A_ReFire
    Goto Ready
  Flash:
    TNT1 A 5 bright A_Light1
    TNT1 A 5 bright A_Light2
    TNT1 A 0 bright A_Light0
    Goto LightDone
  Spawn:
    RAIL A -1
    Stop
  }
}

// DoomSphere
//Credits: The SkullTag Team, and Ghastly_Dragon.
Actor PowerQuadDamageST : PowerDamage
{
	Damagefactor "Normal", 4
}

Actor STDoomsphere : PowerupGiver 5036
{
  +CountItem
  +NoGravity
  +Inventory.AutoActivate
  +Inventory.AlwaysPickup
  Inventory.MaxAmount 0
  Powerup.Type QuadDamageST
  Powerup.Color RedMap
  Inventory.PickupMessage "$PICKUP_DOOMSPHERE"
  States
  {
  Spawn:
    DOOM A 10 Bright
    DOOM B 15 Bright
    DOOM C 8 Bright
    DOOM D 6 Bright
    Loop
  }
}


// This is exactly from SkullTag's\Zandronum's resources; primarily from skulltag_data.pk3.
// All credits for this source should be given to the SkullTag's and Zandronum's
//  Developer Team.
// ThingID and Actor <NAME> has been tweaked to work with the changes since TGRCP1 v1.03.

//=================================================================================================
//
// DarkImp
//
//=================================================================================================

actor TGRDarkImp : DoomImp 6007 // [RT] Inherits from the DoomImp for convenience
{
  game Doom
  spawnid 155 // [RT] I think this is the right Spawnid, change if it isn't
  obituary "$OB_DARKIMP"
  hitobituary "$OB_DARKIMP_MELEE"
  health 200
  missiletype DarkImpBall
  meleedamage 3
  meleesound "imp/melee"
  states
  {
  Spawn:
    DIMP AB 10 A_Look
    loop
  See:
    DIMP AABBCCDD 3 A_Chase
    loop
  Melee:
  Missile:
    DIMP EF 8 A_FaceTarget
    DIMP G 6 A_ComboAttack  // See DarkImpBall
    goto See
  Pain:
    DIMP H 2
    DIMP H 2 A_Pain
    goto See
  Death:
    DIMP I 8
    DIMP J 8 A_Scream
    DIMP K 6
    DIMP L 6 A_NoBlocking
    DIMP M -1
    stop
  XDeath:
    DIMP N 5
    DIMP O 5 A_XScream
    DIMP P 5
    DIMP Q 5 A_NoBlocking
    DIMP RST 5
    DIMP U -1
    stop
  Raise:
    DIMP ML 8
    DIMP KJI 6
    goto See
  }
}

actor DarkImpBall : DoomImpBall // [RT] Inherits from DoomImpBall for convenience.
{
  game Doom
  spawnid 218
  Speed 20
  damage 3 // [RT] I think this is right, change if neccesary.
  states
  {
  Spawn:
    BAL4 AB 4 bright
    loop
  Death:
    BAL4 CDE 6 bright
    stop
  }
}

//=================================================================================================
//
// Cacolantern
//
//=================================================================================================

ACTOR TGRCacolantern : Cacodemon 6008
{
	SpawnID 159
	Health 2000
	Obituary "$OB_CACOLANTERN"
	HitObituary "$OB_CACOLANTERN_MELEE"
	States
	{
	Spawn:
		HED2 A 10 A_Look
		Loop
	See:
		HED2 A 3 A_Chase
		Loop
	Pain:
		HED2 E 3
		HED2 E 3 A_Pain
		HED2 F 6
		Goto See
	Missile:
		HED2 BC 5 A_FaceTarget
		HED2 D 5 BRIGHT A_BasicAttack (8, "None", "CacolanternBall", 32)
		Goto See
	Death:
		HED2 G 8 A_NoBlocking
		HED2 H 8 A_Scream
		HED2 IJK 8
		HED2 L -1 A_SetFloorClip
		Stop
	Raise:
		HED2 L 8 A_UnsetFloorClip
		HED2 KJIHG 8
		Goto See
	}
}

ACTOR CacolanternBall
{
	SpawnID 219
	Radius 6
	Height 8
	Speed 20
	Damage 5
	Projectile
	+RANDOMIZE
	RenderStyle Add
	SeeSound "caco/attack"
	DeathSound "caco/shotx"
	States
	{
	Spawn:
		BAL8 AB 4 BRIGHT
		Loop
	Death:
		BAL8 CDE 6 BRIGHT
		Stop
	}
}

//=================================================================================================
//
// Abaddon
//
//=================================================================================================

ACTOR TGRAbaddon : TGRCacolantern 6002
{
	SpawnID 220
	Health 3000
	Speed 12
	PainChance 40
	Obituary "$OB_ABADDON"
	HitObituary "$OB_ABADDON_MELEE"
	States
	{
	Spawn:
		HED3 A 10 A_Look
		Loop
	See:
		HED3 A 3 A_Chase
		Loop
	Pain:
		HED3 E 3
		HED3 E 3 A_Pain
		HED3 F 6
		Goto See
	Missile:
		HED3 BC 5 A_FaceTarget
		HED3 D 5 BRIGHT A_BasicAttack (8, "None", "AbaddonBall", 32)
		HED3 BC 5 A_FaceTarget
		HED3 D 5 BRIGHT A_BasicAttack (8, "None", "AbaddonBall", 32)
		Goto See
	Death:
		HED3 G 8 A_NoBlocking
		HED3 H 8 A_Scream
		HED3 IJK 8
		HED3 L -1 A_SetFloorClip
		Stop
	Raise:
		HED3 L 8 A_UnSetFloorClip
		HED3 KJIHG 8
		Goto See
	}
}

ACTOR AbaddonBall
{
	SpawnID 221
	Radius 6
	Height 8
	Speed 20
	Damage 10
	Projectile
	+RANDOMIZE
	RenderStyle Add
	SeeSound "caco/attack"
	DeathSound "caco/shotx"
	States
	{
	Spawn:
		BAL3 AB 4 BRIGHT
		Loop
	Death:
		BAL3 CDE 6 BRIGHT
		Stop
	}
}

//=================================================================================================
//
// Belphegor
//
//=================================================================================================

ACTOR TGRBelphegor : HellKnight 6003
{
	SpawnID 215
	Health 3750
	PainChance 25
	SeeSound "baron/sight"
	PainSound "baron/pain"
	DeathSound "baron/death"
	ActiveSound "baron/active"
	Obituary "$OB_BELPHEGOR"
	HitObituary "$OB_BELPHEGOR_MELEE"
	States
	{
	Spawn:
		BOS3 AB 10 A_Look
		Loop
	See:
		BOS3 AABBCCDD 3 A_Chase
		Loop
	Pain:
		BOS3 H 2
		BOS3 H 2 A_Pain
		Goto See
	Melee:
	Missile:
		BOS3 EF 6 A_FaceTarget
		BOS3 G 6 A_BasicAttack (10, "baron/melee", "BelphegorBall", 32)
		BOS3 EF 6 A_FaceTarget
		BOS3 G 6 A_BasicAttack (10, "baron/melee", "BelphegorBall", 32)
		BOS3 EF 6 A_FaceTarget
		BOS3 G 6 A_BasicAttack (10, "baron/melee", "BelphegorBall", 32)
		Goto See
	Death:
		BOS3 I 8
		BOS3 J 8 A_Scream
		BOS3 K 8
		BOS3 L 8 A_NoBlocking
		BOS3 MN 8
		BOS3 O -1 A_BossDeath
		Stop
	Raise:
		BOS3 ONMLKJI 8
		Goto See
	}
}

ACTOR BelphegorBall : BaronBall
{
	SpawnID 222
	Speed 20
}

//=================================================================================================
//
// BloodDemon
//
//=================================================================================================

ACTOR TGRBloodDemon : Demon 6004
{
	SpawnID 156
	Health 750
	Obituary "$OB_BLOODDEMON_MELEE"
	States
	{
	Spawn:
		SRG2 AB 10 A_Look
		Loop
	See:
		SRG2 AABBCCDD 2 A_Chase
		Loop
	Pain:
		SRG2 H 2
		SRG2 H 2 A_Pain
		Goto See
	Melee:
		SRG2 EF 8 A_FaceTarget
		SRG2 G 8 A_SargAttack
		Goto See
	Death:
		SRG2 I 8
		SRG2 J 8 A_Scream
		SRG2 K 4
		SRG2 L 4 A_NoBlocking
		SRG2 M 4
		SRG2 N -1
		Stop
	Raise:
		SRG2 NMLKJI 5
		Goto See
	}
}

//=================================================================================================
//
// Hectebus
//
//=================================================================================================

ACTOR TGRHectebus : Fatso 6005
{
   SpawnID 158
   Health 3000
   PainChance 20
   Obituary "$OB_HECTEBUS"
   States
   {
   Spawn:
      HECT AB 15 A_Look
      Loop
   See:
      HECT AABBCCDDEEFF 4 A_Chase
      Loop
   Pain:
      HECT J 3
      HECT J 3 A_Pain
      Goto See
   Missile:
      //These Next 2 states replace A_FatRaise and A_HectRaise
      HECT G 0 A_PlaySound("fatso/raiseguns")
      HECT G 20 A_FaceTarget
      HECT H 0 A_CustomMissile("HectShot",28,0,0,1)
      HECT H 0 A_CustomMissile("HectShot",28,0,5.625,1)
      HECT H 0 A_CustomMissile("HectShot",28,0,11.25,1)
      HECT H 0 A_CustomMissile("HectShot",28,0,16.875,1)
      HECT H 10 BRIGHT
      HECT IG 5 A_FaceTarget
      HECT H 0 A_CustomMissile("HectShot",28,0,0,1)
      HECT H 0 A_CustomMissile("HectShot",28,0,-5.625,1)
      HECT H 0 A_CustomMissile("HectShot",28,0,-11.25,1)
      HECT H 0 A_CustomMissile("HectShot",28,0,-16.875,1)
      HECT H 10 BRIGHT
      HECT IG 5 A_FaceTarget
      HECT H 0 A_CustomMissile("HectShot",28,0,-11.25,1)
      HECT H 0 A_CustomMissile("HectShot",28,0,-5.625,1)
      HECT H 0 A_CustomMissile("HectShot",28,0,0,1)
      HECT H 0 A_CustomMissile("HectShot",28,0,5.625,1)
      HECT H 0 A_CustomMissile("HectShot",28,0,11.25,1)
      HECT H 10 BRIGHT
      HECT IG 5 A_FaceTarget
      Goto See
   Death:
      HECT K 6
      HECT L 6 A_Scream
      HECT M 6 A_NoBlocking
      HECT NOPQRS 6
      HECT T -1
      Stop
   Raise:
      HECT RQPONMLK 5
      Goto See
   }
}

ACTOR HectShot : FatShot
{
   Speed 22
   Damage 12
   States
   {
   Spawn:
      HECF AB 4 BRIGHT
      Loop
   Death:
      HECF C 8 BRIGHT
      HECF D 6 BRIGHT
      HECF E 4 BRIGHT
      Stop
   }
}

//=================================================================================================
//
// SuperShotgunGuy
//
//=================================================================================================

ACTOR TGRSuperShotgunGuy 6006
{
   Game Doom
   SpawnID 157
   Health 300
   Radius 20
   Height 56
   Mass 100
   Speed 8
   PainChance 170
   SeeSound "chainguy/sight"
   PainSound "chainguy/pain"
   DeathSound "chainguy/death"
   ActiveSound "chainguy/active"
   AttackSound "ssgguy/attack"
   MONSTER
   +FLOORCLIP
   Obituary "$OB_SSGGUY"
   DropItem "SuperShotgun"
   States
   {
      Spawn:
         GPOS AB 10 A_Look
         Loop
      See:
         GPOS AABBCCDD 4 A_Chase
         Loop
      Missile:
         GPOS E 10 A_FaceTarget
         GPOS F 8 Bright A_CustomBulletAttack(8, 7, 7, 5, "BulletPuff")
         GPOS E 8
         Goto See
      Pain:
         GPOS G 3
         GPOS G 3 A_Pain
         Goto See
      Death:
         GPOS H 5
         GPOS I 5 A_Scream
         GPOS J 5 A_NoBlocking
         GPOS KLM 5
         GPOS N -1
         Stop
      XDeath:
         GPOS O 5
         GPOS P 5 A_XScream
         GPOS Q 5 A_NoBlocking
         GPOS RS 5
         GPOS T -1
         Stop
      Raise:
         GPOS LKJIH 5
         Goto See
   }
}


//Statues
ACTOR ImpStatue 5110
{
	Radius 20
	Height 56
	+SOLID
	States
	{
	Spawn:
		STA1 A -1
		Stop
	}
}

ACTOR DemonStatue 5111
{
	Radius 30
	Height 56
	+SOLID
	States
	{
	Spawn:
		STA2 A -1
		Stop
	}
}

ACTOR BaronStatue 5113
{
	Radius 24
	Height 64
	+SOLID
	States
	{
	Spawn:
		STA4 A -1
		Stop
	}
}

ACTOR CyberdemonStatue 5114
{
	Radius 40
	Height 110
	+SOLID
	States
	{
	Spawn:
		STA5 A -1
		Stop
	}
}

//Completely abolish the runes from the maps without having to physically alter
// the maps.
Actor RemoveSkullTagRuneStrength 5100
{
	Radius 0
	Height 0
	States
	{
	Spawn:
		TNT1 A -1
		STOP
	}
}

Actor RemoveSkullTagRuneRage 5101
{
	Radius 0
	Height 0
	States
	{
	Spawn:
		TNT1 A -1
		STOP
	}
}

Actor RemoveSkullTagRuneRegeneration 5105
{
	Radius 0
	Height 0
	States
	{
	Spawn:
		TNT1 A -1
		STOP
	}
}

Actor RemoveSkullTagRuneSpread 5103
{
	Radius 0
	Height 0
	States
	{
	Spawn:
		TNT1 A -1
		STOP
	}
}

// Link these ST bonuses to Doom's standard bonus items
Actor SkullTagArmorBonus : ArmorBonus 5091
{
	//This is only going to inherit Doom's ArmorBonus.
	//This is only a link to another Actor.
}

Actor SkullTagHealthBonus : HealthBonus 5090
{
	//This is only going to inherit Doom's HealthBonus.
	//This is only a link to another Actor.
}

Actor SkullTagRedArmor : BlueArmor 5040
{
	//This is only going to inherit Doom's BlueArmor
	//This is only a link to another Actor.
}